buildscript {
    repositories {
        mavenCentral()
        gradlePluginPortal()
        google()
    }

    apply from: 'buildsystem/dependencies.gradle'

    dependencies {
        classpath "com.android.tools.build:gradle:${versions.androidGradlePlugin}"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}"
        classpath "org.jetbrains.kotlin:kotlin-serialization:${versions.kotlin}"
        classpath "org.jetbrains.kotlinx:binary-compatibility-validator:${versions.binaryCompatibilityValidator}"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:${versions.dokkaGradlePlugin}"
        classpath "org.jlleitschuh.gradle:ktlint-gradle:${versions.ktlintGradle}"
        classpath "com.diffplug.spotless:spotless-plugin-gradle:${versions.spotlessGradlePlugin}"
        classpath "org.jacoco:org.jacoco.core:${versions.jacocoGradlePlugin}"
        classpath "org.jetbrains.kotlinx:atomicfu-gradle-plugin:${versions.atomicFuPlugin}"
        classpath "com.vanniktech:gradle-maven-publish-plugin:${versions.mavenPublishPlugin}"
        classpath("app.cash.zipline:zipline-gradle-plugin:0.9.1")
        classpath("com.squareup.sqldelight:gradle-plugin:1.5.3")
        classpath("com.google.dagger:hilt-android-gradle-plugin:2.42")
    }
}

apply from: 'buildsystem/dependencies.gradle'

apply plugin: 'binary-compatibility-validator'

apiValidation {
    ignoredProjects += ["reddit", "store", "samples", "notes", "android", "common", "fig"]
}

allprojects {
    repositories {
        mavenCentral()
        google()
        jcenter()
    }

    apply plugin: 'org.jlleitschuh.gradle.ktlint'
    ktlint {
        version = versions.ktlint
        disabledRules = ["import-ordering"]
    }


    // Workaround to prevent Gradle from stealing focus from other apps during tests run/etc.
    // https://gist.github.com/artem-zinnatullin/4c250e04636e25797165
    tasks.withType(JavaForkOptions) {
        jvmArgs '-Djava.awt.headless=true'
    }
}

subprojects {
    apply plugin: 'com.diffplug.spotless'
    spotless {
        kotlin {
            target 'src/**/*.kt'
        }
    }
}

allprojects {
    plugins.withId("com.vanniktech.maven.publish") {
        mavenPublish {
            sonatypeHost = "S01"
        }
    }
}
